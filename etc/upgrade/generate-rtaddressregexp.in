#!@PERL@
# BEGIN BPS TAGGED BLOCK {{{
#
# COPYRIGHT:
#
# This software is Copyright (c) 1996-2018 Best Practical Solutions, LLC
#                                          <sales@bestpractical.com>
#
# (Except where explicitly superseded by other copyright notices)
#
#
# LICENSE:
#
# This work is made available to you under the terms of Version 2 of
# the GNU General Public License. A copy of that license should have
# been provided with this software, but in any event can be snarfed
# from www.gnu.org.
#
# This work is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
# 02110-1301 or visit their web page on the internet at
# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
#
#
# CONTRIBUTION SUBMISSION POLICY:
#
# (The following paragraph is not intended to limit the rights granted
# to you to modify and distribute this software under the terms of
# the GNU General Public License and is only of importance to you if
# you choose to contribute your changes and enhancements to the
# community by submitting them to Best Practical Solutions, LLC.)
#
# By intentionally submitting any modifications, corrections or
# derivatives to this work, or any other work intended for use with
# Request Tracker, to Best Practical Solutions, LLC, you confirm that
# you are the copyright holder for those contributions and you grant
# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
# royalty-free, perpetual, license to use, copy, create derivative
# works based on those contributions, and sublicense and distribute
# those contributions and any derivatives thereof.
#
# END BPS TAGGED BLOCK }}}
use 5.10.1;
use strict;
use warnings;

use lib "@LOCAL_LIB_PATH@";
use lib "@RT_LIB_PATH@";

use RT::Interface::CLI qw(Init);
Init();

if (my $re = RT->Config->Get('RTAddressRegexp')) {
    print "No need to use this script, you already have RTAddressRegexp set to $re\n";
    exit;
}

use RT::Queues;
my $queues = RT::Queues->new( RT->SystemUser );
$queues->UnLimit;

my %merged;
merge(\%merged, RT->Config->Get('CorrespondAddress'), RT->Config->Get('CommentAddress'));
while ( my $queue = $queues->Next ) {
    merge(\%merged, $queue->CorrespondAddress, $queue->CommentAddress);
}

my @domains;
for my $domain (sort keys %merged) {
    my @addresses;
    for my $base (sort keys %{$merged{$domain}}) {
        my @subbits = keys(%{$merged{$domain}{$base}});
        if (@subbits > 1) {
            push @addresses, "\Q$base\E(?:".join("|",@subbits).")";
        } else {
            push @addresses, "\Q$base\E$subbits[0]";
        }
    }
    if (@addresses > 1) {
        push @domains, "(?:".join("|", @addresses).")\Q\@$domain\E";
    } else {
        push @domains, "$addresses[0]\Q\@$domain\E";
    }
}
my $re = join "|", @domains;

print <<ENDDESCRIPTION;
You can add the following to RT_SiteConfig.pm, but may want to collapse it into a more efficient regexp.
Keep in mind that this only contains the email addresses that RT knows about, you should also examine
your mail system for aliases that reach RT but which RT doesn't know about.
ENDDESCRIPTION
print "Set(\$RTAddressRegexp,qr{^(?:${re})\$}i);\n";

sub merge {
    my $merged = shift;
    for my $address (grep {defined and length} @_) {
        $address =~ /^\s*(.*?)(-comments?)?\@(.*?)\s*$/;
        $merged->{lc $3}{$1}{$2||''}++;
    }
}
