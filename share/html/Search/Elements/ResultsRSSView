%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2018 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<%INIT>
my $Tickets = RT::Tickets->new($session{'CurrentUser'});
$Tickets->FromSQL($ARGS{'Query'});
if ($OrderBy =~ /\|/) {
    # Multiple Sorts
    my @OrderBy = split /\|/,$OrderBy;
    my @Order = split /\|/,$Order;
    $Tickets->OrderByCols(
        map { { FIELD => $OrderBy[$_], ORDER => $Order[$_] } } ( 0
        .. $#OrderBy ) );;
} else {
    $Tickets->OrderBy(FIELD => $OrderBy, ORDER => $Order);
}
$r->content_type('application/rss+xml; charset=utf-8');

use XML::RSS;
my $rss = XML::RSS->new(version => '1.0');

my $url;
if ( RT->Config->Get('CanonicalizeURLsInFeeds') ) {
    $url = RT->Config->Get('WebURL');
} else {
    $url = RT::Interface::Web::GetWebURLFromRequest();
}


my $base_date = RT::Date->new( RT->SystemUser );
$base_date->SetToNow;
$base_date->SetToMidnight;

$rss->channel(
    title        => RT->Config->Get('rtname').": Search " . $ARGS{'Query'},
    link         => $url,
    description  => "",
    dc           => { },
    generator    => "RT v" . $RT::VERSION,
    syn => {
        updatePeriod     => "hourly",
        updateFrequency  => "1",
        updateBase       => $base_date->W3CDTF,
    },
);


while ( my $Ticket = $Tickets->Next()) {
    my $creator_str = $Ticket->CreatorObj->Format;
    $creator_str =~ s/[\r\n]//g;

    # Get the plain-text content; it is interpreted as HTML by RSS
    # readers, so it must be escaped (and is escaped _again_ when
    # inserted into the XML).
    my $content = $Ticket->Transactions->First->Content;
    $content = $m->interp->apply_escapes( $content, 'h');

    $rss->add_item(
        title       => $Ticket->Subject || loc('No Subject'),
        link        => $url . "Ticket/Display.html?id=".$Ticket->id,
        description => $content,
        dc          => {
            creator => $creator_str,
            date    => $Ticket->CreatedObj->W3CDTF,
        },
        guid        => $Ticket->Queue . '_' . $Ticket->id,
    );
}

$m->out($rss->as_string);
$m->abort();
</%INIT>
<%ARGS>
$OrderBy => 'Created'
$Order => 'ASC'
</%ARGS>

