%# BEGIN BPS TAGGED BLOCK {{{
%# 
%# COPYRIGHT:
%# 
%# This software is Copyright (c) 1996-2008 Best Practical Solutions, LLC
%#                                          <jesse@bestpractical.com>
%# 
%# (Except where explicitly superseded by other copyright notices)
%# 
%# 
%# LICENSE:
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%# 
%# 
%# CONTRIBUTION SUBMISSION POLICY:
%# 
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%# 
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%# 
%# END BPS TAGGED BLOCK }}}
<& /Elements/Header, title => $title, refresh => $session{'tickets_refresh_interval'}, 
    rss_auto_discovery => $RSSFeedURL,
    link_rel => \%link_rel &>
<& /Ticket/Elements/Tabs, 
    current_tab => "Search/Results.html".$query_string, 
    title => $title,
    format => $format,
    query => $query,
    rows => $rows,
    order_by => $order_by,
    order => $order,
    saved_searchid => $saved_searchid &>
<& /Elements/CollectionList, 
    query => $query,
    allow_sorting => 1,
    order_by => $order_by,
    order => $order,
    rows => $rows,
    page => $page,
    format => $format,
    base_url => RT->config->get('WebPath')."/Search/Results.html?",
    class => 'RT::Model::TicketCollection',

   &>
% my %hiddens = (query => $query, format => $format, rows => $rows, order_by => $order_by, order => $order, HideResults => $hide_results, page => $page );
<div align="right" class="refresh">
<form method="get" action="<%RT->config->get('WebPath')%>/Search/Results.html">
% foreach my $key (keys(%hiddens)) {
<input type="hidden" class="hidden" name="<%$key%>" value="<% defined($hiddens{$key})?$hiddens{$key}:'' %>" />
% }
<& /Elements/Refresh, name => 'TicketsRefreshInterval', default => $session {'tickets_refresh_interval'} &>
<input type="submit" class="button" value="<&|/l&>Change</&>" />
</form>
</div>
<div align="right">
<& Elements/ResultViews,
    query_string => $query_string,
    query => $query,
    format => $format,
    rows => $rows,
    order_by => $order_by,
    order => $order,
    rss_feed_url => $RSSFeedURL,
    short_querystring => $short_querystring,
&>

% $m->callback( query_string => $query_string, Callbackname => 'SearchActions' );
</div>
<%INIT>
# Read from user preferences
my $prefs = Jifty->web->current_user->user_object->preferences("SearchDisplay") || {};
# These variables are what define a search_hash; this is also
# where we give sane defaults.
$format      ||= $prefs->{'format'} || RT->config->get('DefaultSearchResultFormat');
$order       ||= $prefs->{'order'} || 'ASC';
$order_by     ||= $prefs->{'order_by'} || 'id';

# Some forms pass in "rows_per_page" rather than "Rows"
# We call it rows_per_page everywhere else.

if ( !defined($rows) ) {
    if (defined $ARGS{'rows_per_page'} ) {
        $rows = $ARGS{'rows_per_page'};
    } elsif ( defined $prefs->{'rows_per_page'} ) {
        $rows = $prefs->{'rows_per_page'};
    } else {
        $rows = 50;
    }
}

my ($title, $ticketcount);
$session{'i'}++;
$session{'tickets'} = RT::Model::TicketCollection->new() ;
$session{'tickets'}->from_sql($query) if ($query);

if ($order_by =~ /\|/) {
    # Multiple Sorts
    my @order_by = split /\|/,$order_by;
    my @order = split /\|/,$order;
    $session{'tickets'}->order_by(
        map { { column => $order_by[$_], order => $order[$_] } } ( 0
        .. $#order_by ) );; 
} else {
    $session{'tickets'}->order_by(column => $order_by, order => $order); 
}

$session{'CurrentSearchHash'} = {
    format      => $format,
    query       => $query,
    page       => $page,
    order       => $order,
    order_by     => $order_by,
    rows_per_page => $rows
    };


if ( $session{'tickets'}->query()) {
    $ticketcount = $session{tickets}->count_all();
    $title = _('Found %quant(%1,ticket)', $ticketcount);
} else {
    $title = _("Find tickets");
}

my $query_string = "?".$m->comp('/Elements/QueryString',
                               query => $query,
                               format => $format,
                               rows => $rows,
                               order_by => $order_by,
                               order => $order,
                               page => $page);
my $short_querystring = "?".$m->comp('/Elements/QueryString', query => $query);
my $RSSQueryString = "?".$m->comp('/Elements/QueryString', query => $query, order => $order, order_by => $order_by);
my $RSSFeedURL = RT->config->get('WebPath')."/Search/Results.rdf$RSSQueryString";

if ($ARGS{'TicketsRefreshInterval'}) {
	$session{'tickets_refresh_interval'} = $ARGS{'TicketsRefreshInterval'};
}

my %link_rel;
my $genpage = sub {
    return $m->comp(
        '/Elements/QueryString',
        query   => $query,
        format  => $format,
        rows    => $rows,
        order_by => $order_by,
        order   => $order,
        page    => shift(@_),
    );
};
my $BaseURL = RT->config->get('WebPath')."/Search/Results.html?";
$link_rel{first} = $BaseURL . $genpage->(1)         if $page > 1;
$link_rel{prev}  = $BaseURL . $genpage->($page - 1) if $page > 1;
$link_rel{next}  = $BaseURL . $genpage->($page + 1) if ($page * $rows) < $ticketcount;
$link_rel{last}  = $BaseURL . $genpage->(POSIX::ceil($ticketcount/$rows)) if $rows and ($page * $rows) < $ticketcount;
</%INIT>
<%CLEANUP>
$session{'tickets'}->prep_for_serialization();
</%CLEANUP>
<%ARGS>
$query => undef
$format => undef 
$hide_results => 0
$rows => undef
$page => 1
$order_by => undef
$order => undef
$saved_searchid => undef
</%ARGS>
