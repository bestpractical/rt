%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2018 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<& /Elements/Header, Title => $title &>
<& /Elements/Tabs
&>
<& /Elements/ListActions, actions => \@results &>

<br />

% for my $pane ( @panes ) {
<&|/Widgets/TitleBox,
    title => loc('RT at a glance') .': '. loc( $pane->{Name} ),
    bodyclass => ""
&>
<& /Widgets/SelectionBox:show, self => $pane &>
</&>
% }

<&|/Widgets/TitleBox, title => loc('Options'), bodyclass => "" &>
<form method="post" action="MyRT.html">
<&|/l&>Rows per box</&>:
<input name="SummaryRows" value="<% $ARGS{SummaryRows} %>" />
<input type="submit" class="button" name="UpdateSummaryRows" value="<% loc('Save') %>" />
</form>
</&>
<&|/Widgets/TitleBox, title => loc("Reset RT at a glance") &>
<form method="post" action="MyRT.html">
<input type="hidden" name="Reset" value="1" />
<input type="submit" class="button" value="<% loc('Reset to default') %>">
</form>
</&>


<%INIT>
my @results;
my $title = loc("Customize").' '.loc("RT at a glance");
my $user = $session{'CurrentUser'}->UserObj;

if ( $ARGS{'UpdateSummaryRows'} ) {
    unless ( $ARGS{SummaryRows} && int $ARGS{SummaryRows} > 0 ) {
        push @results, loc ("Illegal '[_1]' preference value.", loc('summary rows'));
        $ARGS{SummaryRows} = 0;
    }
    else {
        my ($ok, $msg) = $user->SetPreferences( 'SummaryRows', int $ARGS{SummaryRows} );
        push @results, $ok ? loc('Preferences saved for [_1].', loc('summary rows')) : $msg;
    }
}
$ARGS{'SummaryRows'} ||= $user->Preferences('SummaryRows', RT->Config->Get('DefaultSummaryRows'));

if ($ARGS{Reset}) {
    for my $pref_name ('HomepageSettings', 'SummaryRows') {
        next unless $user->Preferences($pref_name);
        my ($ok, $msg) = $user->DeletePreferences($pref_name);
        push @results, $msg unless $ok;
    }
    push @results, loc('Preferences saved.') unless @results;
}

my $portlets = $user->Preferences('HomepageSettings');
unless ($portlets) {
    my ($defaults) = RT::System->new($session{'CurrentUser'})->Attributes->Named('HomepageSettings');
    $portlets = $defaults ? $defaults->Content : {};
}

my %seen;
my @items = map ["component-$_", loc($_)], grep !$seen{$_}++, @{RT->Config->Get('HomepageComponents')};

my $sys = RT::System->new($session{'CurrentUser'});
my @objs = ($sys);

push @objs, RT::SavedSearch->new( $session{CurrentUser} )->ObjectsForLoading
    if $session{'CurrentUser'}->HasRight( Right  => 'LoadSavedSearch',
                                          Object => $RT::System );

my @sys_searches;
for my $object (@objs) {
    for ($m->comp("/Search/Elements/SearchesForObject", Object => $object)) {
        my ($desc, $loc_desc, $search) = @$_;

        my $SearchType = 'Ticket';
        if ((ref($search->Content)||'') eq 'HASH') {
            $SearchType = $search->Content->{'SearchType'}
                if $search->Content->{'SearchType'};
        }
        else {
            $RT::Logger->debug("Search ".$search->id." ($desc) appears to have no Content");
        }

        if ($object eq $sys && $SearchType eq 'Ticket') {
            push @items, ["system-$desc", $loc_desc];
            push @sys_searches, [$desc, $search];
        }
        else {
            my $oid = ref($object).'-'.$object->Id.'-SavedSearch-'.$search->Id;
            my $type = ($SearchType eq 'Ticket')
                ? 'Saved Search' # loc
                : $SearchType;
            push @items, ["saved-$oid", loc($type).": $loc_desc"];
        }
    }
}

my @panes = $m->comp(
    '/Admin/Elements/ConfigureMyRT',
    panes  => ['body', 'sidebar'],
    Action => 'MyRT.html',
    items => \@items,
    current_portlets => $portlets,
    OnSave => sub {
        my ( $conf, $pane ) = @_;
        my ($ok, $msg) = $user->SetPreferences( 'HomepageSettings', $conf );
        push @results, $ok ? loc('Preferences saved for [_1].', $pane) : $msg;
    }
);

$m->comp( '/Widgets/SelectionBox:process', %ARGS, self => $_ )
    for @panes;

</%INIT>
