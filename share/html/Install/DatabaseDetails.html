%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2018 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<&| Elements/Wrapper, Title => loc('Step [_1] of [_2]', 2, 7 ) .': '. loc('Check Database Credentials') &> 

% if ( @errors ) {
<& Elements/Errors, Errors => \@errors &>
<p>
<&|/l&>Tell us a little about how to find the database RT will be using</&>
<p>
<&|/l&>We need to know the name of the database RT will use and where to find it.  We also need to know the username and password of the user RT should use. RT can create the database and user for you, which is why we ask for the username and password of a user with DBA privileges.  During step 6 of the installation process we will use this information to create and initialize RT's database.</&>
</p>
<p>
<&|/l&>When you click on 'Check Database Connectivity' there may be a small delay while RT tries to connect to your database</&>

% } elsif ( @results ) {
<& /Elements/ListActions, actions => \@results &>

<p>
<&|/l&>We are able to find your database and connect as the DBA.  You can click on 'Customize Basics' to continue customizing RT.</&>

% }

<form method="post">
<input type="hidden" name="Run" value="1">
% if ( @results && !@errors ) {
<& /Elements/Submit,
    Label => loc('Next') .': '. loc('Customize Basics'),
    Back => 1, BackLabel => loc('Back') .': '. loc('Select Database Type'),
    Name => 'Next',
&>
% } else {
<& /Widgets/BulkEdit, Types => \@Types, Meta => $RT::Installer->{Meta},
    CurrentValue => { %{RT::Installer->CurrentValues(@Types)},
        DatabaseAdmin =>
            RT::Installer->CurrentValue( 'DatabaseAdmin' ) ||
            $RT::Installer->{InstallConfig}{DatabaseAdmin} ||
            ( $db_type eq 'mysql' ? 'root'
              : $db_type eq 'Pg' ? 'postgres'
              : '' ),
        }
                    &>
<& /Elements/Submit,
    Label => loc('Check Database Connectivity'),
    Back => 1, BackLabel => loc('Back') .': '. loc('Choose Database Engine'),
&>
% }

</form>
</&>

<%init>
my (@results, @errors);
my $ConnectionSucceeded;

my @Types = 'DatabaseName';

my $db_type = $RT::Installer->{InstallConfig}{DatabaseType};

unless ( $db_type eq 'SQLite' ) {
    push @Types, 'DatabaseHost', 'DatabasePort', 'DatabaseAdmin',
        'DatabaseAdminPassword', 'DatabaseUser', 'DatabasePassword';
}


if ( $Run ) {

    if ( $Back ) {
        RT::Interface::Web::Redirect(RT->Config->Get('WebURL') .
'Install/DatabaseType.html');
    }
    
    if ( $ARGS{Next} ) {
        RT::Interface::Web::Redirect(RT->Config->Get('WebURL') .
'Install/Basics.html');
    }

    $m->comp('/Widgets/BulkProcess', Types => \@Types, Arguments => \%ARGS, 
            Store => $RT::Installer->{InstallConfig}, 
            Meta => $RT::Installer->{Meta}, KeepUndef => 1 ); 


    my ( $status, $msg ) = RT::Installer->SaveConfig;
    if ( $status ) {
        RT->LoadConfig;
        RT::Handle->FinalizeDatabaseType();
    # dba connect systemdsn
        my $dbh = DBI->connect(
            RT::Handle->SystemDSN, $ARGS{DatabaseAdmin}, $ARGS{DatabaseAdminPassword}, { RaiseError => 0, PrintError => 0 },
        );
    
        if ( $dbh ) {
            push @results, loc('Connection succeeded');
            # dba connect dsn, which has table info
            $dbh = DBI->connect(
                RT::Handle->DSN, $ARGS{DatabaseAdmin}, $ARGS{DatabaseAdminPassword}, { RaiseError => 0, PrintError => 0 },
            );

            if ( $dbh and $db_type eq "Oracle") {
                # The database _existing_ is itself insufficient for Oracle -- we need to check for the RT user
                my $sth = $dbh->prepare('SELECT username FROM dba_users WHERE username = ?');
                $sth->execute( $ARGS{DatabaseUser} );
                undef $dbh unless $sth->fetchrow_array;

                push @errors, loc("Oracle users cannot have empty passwords")
                    unless $ARGS{DatabasePassword};
            }

            if ( $dbh ) {
                # check if table Users exists
                eval {
                    my $dbh = DBI->connect(
                        RT::Handle->DSN,
                        $ARGS{DatabaseAdmin},
                        $ARGS{DatabaseAdminPassword},
                        { RaiseError => 1, PrintError => 0 },
                    );

                    my $sth = $dbh->prepare('select * from Users');
                    $sth->execute();
                };

                unless ( $@ ) {
    
                    my $sth = $dbh->prepare('select id from Users where Name=?');
                    $sth->execute('RT_System'); 
                    if ( $sth->fetchrow_array ) {
                        $RT::Installer->{DatabaseAction} = 'none';
                        push @results, loc("[_1] appears to be fully initialized.  We won't need to create any tables or insert metadata, but you can continue to customize RT by clicking 'Customize Basics' below", $RT::DatabaseName);
                    }
                    else {
                        $RT::Installer->{DatabaseAction} = 'acl,coredata,insert';
                        push @results, loc("[_1] already exists and has RT's tables in place, but does not contain RT's metadata. The 'Initialize Database' step later on can insert metadata into this existing database. If this is acceptable, click 'Customize Basics' below to continue customizing RT.", $RT::DatabaseName);
                    }
                }
                else {
                    $RT::Installer->{DatabaseAction} = 'schema,acl,coredata,insert';
                    push @results, loc("[_1] already exists, but does not contain RT's tables or metadata. The 'Initialize Database' step later on can insert tables and metadata into this existing database. if this is acceptable, click 'Customize Basic' below to continue customizing RT.", $RT::DatabaseName );
                }

            }
            else {
                $RT::Installer->{DatabaseAction} =
    'create,schema,acl,coredata,insert';
            }
        }
        else {
            $RT::Installer->{DatabaseAction} = 'error';
            push @errors, loc("Failed to connect to database: [_1]",
                    $DBI::errstr );
        }
    }
    else {
        push @results, loc($msg);
    }

}

</%init>

<%args>
$Run => undef
$Back => undef
</%args>
