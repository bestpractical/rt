%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2025 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<& /SelfService/Elements/Header, Title => loc('Preferences') &>

<& /Elements/ListActions, actions => \@results &>
% if ( $pref eq 'full-edit' ) {
<& /Prefs/Elements/EditAboutMe, UserObj => $user, PasswordName => $password, AccessControlName => 'Change Password' &>
% }
% if ( $pref eq 'edit-prefs' || $pref eq 'edit-prefs-view-info' ) {
<form method="post" action="">
  <div class="row mt-2">

    <div class="col-6">
      <&| /Widgets/TitleBox, title => loc('Locale'), id => "user-prefs-identity" &>
        <&| /Elements/LabeledValue, Label => loc("Language") &>
            <& /Elements/SelectLang, Name => 'Lang', Default => $user->Lang &>
        </&>

        <&| /Elements/LabeledValue, Label => loc("Timezone") &>
          <& /Elements/SelectTimezone, Name => 'Timezone', Default => $user->Timezone &>
        </&>
      </&>
    </div>

    <div class="col-6">
      <&| /Widgets/TitleBox, title => loc('Change password')  &>
% if ( $user->__Value('Password') ne '*NO-PASSWORD*' ) {
        <& /Elements/EditPassword,
            User => $user,
            Name => $password,
        &>
% }
</&>
    </div>
  </div>

  <& /Elements/Submit, Label => loc('Save Changes'), FullWidth => 1 &>
</form>

% }
% if ( $pref eq 'view-info' || $pref eq 'edit-prefs-view-info' ) {
<& /Prefs/Elements/ShowAboutMe, UserObj => $user &>
%    if ( $request_queue->id ) {
<& Elements/RequestUpdate, User=> $user &>
%    }
% }

% if( RT->Config->Get('SelfServiceDownloadUserData') ) {
<div class="row mt-2">
  <div class="col-6">
    <& /SelfService/User/Elements/RelatedData, UserObj => $user &>
  </div>
</div>
% }

<%INIT>
my @results;

my $user = $session{'CurrentUser'}->UserObj;
my $password = [ qw(CurrentPass NewPass1 NewPass2) ];

my $pref = RT->Config->Get( 'SelfServiceUserPrefs' ) || '';
my $request_queue = RT::Queue->new( $session{CurrentUser} );

if ( my $queue_name = RT->Config->Get('SelfServiceRequestUpdateQueue') ) {
    $request_queue->Load($queue_name);
    unless ( $request_queue->id ) {
        RT->Logger->error("Couldn't load $queue_name");
    }
}

if ( $pref eq 'full-edit' && $DeleteImage && RT->Config->Get('SelfServiceShowUserImages') ) {
    my ( $status, $msg ) = $user->SetImageAndContentType( '', '' );

    if ($status) {
        push @results, loc('Image deleted');
    }
    else {
        push @results, loc('Error: Unable to delete image');
        RT->Logger->error( 'Unable to delete image for user ' . $user->Id . " $msg" );
    }
}
elsif ( $pref eq 'edit-prefs' || $pref eq 'edit-prefs-view-info' || $pref eq 'full-edit' ) {

    if ( defined $NewPass1 && length $NewPass1 ) {
        my ($status, $msg) = $user->SafeSetPassword(
            Current      => $CurrentPass,
            New          => $NewPass1,
            Confirmation => $NewPass2,
        );
        push @results, loc("Password: [_1]", $msg);
    }

    my @fields;

    if ( $pref eq 'edit-prefs' || $pref eq 'edit-prefs-view-info' ) {
        @fields = qw( Lang Timezone );
    } else {
        @fields = qw(
            Name Comments EmailAddress FreeformContactInfo Organization RealName
            NickName Lang Gecos HomePhone WorkPhone MobilePhone PagerPhone Address1
            Address2 City State Zip Country Timezone
        );
    }

    $m->callback(
        CallbackName => 'UpdateLogic',
        fields       => \@fields,
        results      => \@results,
        UserObj      => $user,
        ARGSRef      => \%ARGS,
    );

    push @results, UpdateRecordObject (
        AttributesRef => \@fields,
        Object => $user,
        ARGSRef => \%ARGS,
    );

    push @results, ProcessObjectCustomFieldUpdates( ARGSRef => \%ARGS, Object => $user ) if $pref eq 'full-edit';

    if ( $pref eq 'full-edit' && $UserImage && RT->Config->Get('SelfServiceShowUserImages') ) {
        if ( my $file_hash = _UploadedFile('UserImage') ) {
            if ( length $file_hash->{LargeContent} > RT->Config->Get('MaxUserImageSize') ) {
                push @results,
                    loc( 'Image too large, max is [_1]',
                        sprintf( '%.1fMB', RT->Config->Get('MaxUserImageSize') / 1024**2 ) );
            }
            else {
                my ( $status, $msg )
                    = $user->SetImageAndContentType( $file_hash->{LargeContent}, $file_hash->{ContentType} );
                push @results, $msg;
            }
        }
    }

    if ( $Lang ) {
        $session{'CurrentUser'}->LanguageHandle($Lang);
        RT::Interface::Web::Session::Set(
            Key   => 'CurrentUser',
            Value => $session{'CurrentUser'},
        );
    }
}


if ( $request_queue->id ) {
    my $path = RT->Config->Get('WebPath') . '/SelfService/Prefs.html';
    push @results,
      ProcessQuickCreate(
        Path    => $path,
        ARGSRef => { %ARGS, Queue => $request_queue->id, Requestors => $session{CurrentUser}->EmailAddress },
      );
}

#A hack to make sure that session gets rewritten.
$session{'i'}++;

MaybeRedirectForResults(
    Actions   => \@results,
);

</%INIT>

<%ARGS>
$CurrentPass => undef
$NewPass1 => undef
$NewPass2 => undef
$Lang => undef
$UserImage => undef
$DeleteImage => undef
</%ARGS>
