%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2025 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<& /Elements/Header,
    Title => $title,
&>
<& /Elements/Tabs &>
    
<& /Elements/ListActions, actions => \@results &>

<form action="<% RT->Config->Get('WebPath') %>/Ticket/Create.html" method="post" enctype="multipart/form-data" name="TicketCreate">
  <input type="hidden" class="hidden" name="id" value="new" />
  <input type="hidden" class="hidden" name="Token" value="<% $ARGS{'Token'} %>" />
  <input type="hidden" name="QueueChanged" value="0" />

% if ( $ARGS{'AddGroupCc'} ){
<input type="hidden" class="hidden" name="AddGroupCc" value="<% $ARGS{'AddGroupCc'} %>" />
% }

% if ( $ARGS{'ChangedField'} ) {
%   for my $field ( ref $ARGS{ChangedField} eq 'ARRAY' ? @{ $ARGS{ChangedField} } : $ARGS{ChangedField} ) {
  <input type="hidden" class="hidden" name="ChangedField" value="<% $field %>" />
%   }
% }

%# If user includes an article, it usually means the content changes.
% if ( $ARGS{IncludeArticleId} ) {
  <input type="hidden" class="hidden" name="ChangedField" value="Content" />
% }

% $m->callback( CallbackName => 'FormStart', QueueObj => $QueueObj, ARGSRef => \%ARGS );

% if ($gnupg_widget) {
  <& /Elements/Crypt/SignEncryptWidget:ShowIssues, self => $gnupg_widget, Queue => $QueueObj &>
% }

<div id="Ticket-Create-basics">
<a name="basics"></a>

<div id="ticket-create-metadata">
    <&| /Widgets/TitleBox, title => loc("Basics"), class=>'ticket-info-basics' &>
    <div>
    <& /Ticket/Elements/EditBasics,
        InTable => 1,
        QueueObj => $QueueObj,
        defaults => \%ARGS,
        fields  => [
            {   name => 'Queue',
                comp => '/Elements/SelectQueue',
                args => {
                    Name           => 'Queue',
                    Default        => $Queue,
                    ShowNullOption => 0,
                    AutoSubmit     => 1,
                    ShowAllQueues  => 0,
                },
            },
            {   name => 'Status',
                comp => '/Ticket/Elements/SelectStatus',
                args => {
                    Name            => "Status",
                    QueueObj        => $QueueObj,
                },
            },
            {   name => 'Owner',
                comp => '/Elements/SelectOwner',
                args => {
                    Name            => "Owner",
                    Default         => $ARGS{Owner} || RT->Nobody->Id,
                    DefaultValue    => 0,
                    QueueObj        => $QueueObj,
                },
            },

            { special => 'roles' },

            $QueueObj->SLADisabled ? () : (
            {   name => 'SLA',
                comp => '/Elements/SelectSLA',
                args => {
                    Name            => "SLA",
                    Default         => $ARGS{SLA} || RT::SLA->GetDefaultServiceLevel(Queue => $QueueObj),
                    DefaultValue    => RT::SLA->GetDefaultServiceLevel(Queue => $QueueObj) ? 0 : 1,
                    QueueObj        => $QueueObj,
                },
            }),
        ]
        &>

% $m->callback( CallbackName => 'AfterOwner', ARGSRef => \%ARGS );

      <& /Elements/EditCustomFields,
          %ARGS,
          Object => $ticket,
          CategoryObj => $QueueObj,
          CustomFields => $QueueObj->TicketCustomFields,
          Grouping => 'Basics',
          InTable => 1,
          ForCreation => 1,
      &>
      <& /Ticket/Elements/EditTransactionCustomFields, %ARGS, QueueObj => $QueueObj, InTable => 1 &>
    </div>
  </&>

<& /Ticket/Elements/ShowAssetsOnCreate, QueueObj => $QueueObj, ARGSRef => \%ARGS &>

% $m->callback( CallbackName => 'AfterBasics', QueueObj => $QueueObj, ARGSRef => \%ARGS );

<& /Elements/EditCustomFieldCustomGroupings,
    %ARGS,
    Object => $ticket,
    CategoryObj => $QueueObj,
    CustomFieldGenerator => sub { $QueueObj->TicketCustomFields },
    ForCreation => 1,
&>

</div>

<div id="ticket-create-message">
  <&| /Widgets/TitleBox, title => $title, class => 'messagedetails' &>
<div>
% $m->callback(CallbackName => 'BeforeRequestors', QueueObj => $QueueObj, ARGSRef => \%ARGS);

<div class="form-row">
<div class="label col-2"><&|/l&>Requestors</&>:</div>
<div class="value col-9">
<& /Elements/EmailInput, Name => 'Requestors', Size => undef, Default => $ARGS{Requestors} // $session{CurrentUser}->EmailAddress, AutocompleteMultiple => 1, AutocompleteType => 'Principals' &>
% $m->callback( CallbackName => 'AfterRequestors', QueueObj => $QueueObj, ARGSRef => \%ARGS );
</div>
</div>

<div class="form-row">
<div class="label col-2">
<span class="prev-icon-helper"><&|/l&>Cc</&>:</span>\
<span class="far fa-question-circle icon-helper" data-toggle="tooltip" data-placement="top" data-original-title="<&|/l&>Sends a copy of this update to the provided users or groups. These people will receive future updates.</&>"></span>
</div>
<div class="value col-9"><& /Elements/EmailInput, Name => 'Cc', Size => undef, Default => $ARGS{Cc}, AutocompleteMultiple => 1, AutocompleteType => 'Principals' &></div>
</div>

<div class="form-row">
<div class="label col-2">
<span class="prev-icon-helper"><&|/l&>Admin Cc</&>:</span>\
<span class="far fa-question-circle icon-helper" data-toggle="tooltip" data-placement="top" data-original-title="<&|/l&>Sends a copy of this update to the provided users or groups. These people will receive future updates.</&>"></span>
</div>
<div class="value col-9"><& /Elements/EmailInput, Name => 'AdminCc', Size => undef, Default => $ARGS{AdminCc}, AutocompleteMultiple => 1, AutocompleteType => 'Principals' &></div>
</div>

% my $roles = $QueueObj->CustomRoles;
% $roles->LimitToMultipleValue;

% my @hidden = $QueueObj->HiddenCustomRoleIDsForURL;
% $roles->Limit(FIELD => 'id', OPERATOR => 'NOT IN', VALUE => \@hidden) if @hidden;

% $m->callback( CallbackName => 'ModifyCustomRoles', ARGSRef => \%ARGS, CustomRoles => $roles );
% while (my $role = $roles->Next) {
<div class="form-row">
  <div class="label col-2">
    <span class="<% $role->EntryHint ? 'prev-icon-helper' : '' %>"><% $role->Name %>:</span>\
% if ($role->EntryHint) {
<span class="far fa-question-circle icon-helper" data-toggle="tooltip" data-placement="top" data-original-title="<% $role->EntryHint %>"></span>
% }
  </div>
  <div class="value col-9"><& /Elements/MultiUserRoleInput, role => $role, Default => $ARGS{$role->GroupType} &></div>
</div>
% }

<& /Elements/EditCustomFields,
    %ARGS,
    Object => $ticket,
    CategoryObj => $QueueObj,
    CustomFields => $QueueObj->TicketCustomFields,
    Grouping => 'People',
    InTable => 1,
    ForCreation => 1,
&>

<div class="form-row">
<div class="label col-2">
<&|/l&>Subject</&>:
</div>
<div class="value col-9">
<input class="form-control" type="text" name="Subject" maxsize="200" value="<%$ARGS{Subject} || ''%>" />
% $m->callback( %ARGS, CallbackName => 'AfterSubject' );
</div>
</div>

% if ( $gnupg_widget ) {
<div class="form-row">
  <div class="label col-2 empty"></div>
  <div class="value col-9"><& /Elements/Crypt/SignEncryptWidget, self => $gnupg_widget, QueueObj => $QueueObj &></div>
</div>
% }
% if ( RT->Config->Get('ArticleOnTicketCreate')) {
<& /Articles/Elements/BeforeMessageBox, %ARGS, QueueObj => $QueueObj &>
% }

% $m->callback( %ARGS, QueueObj => $QueueObj, CallbackName => 'BeforeMessageBox' );
% if (exists $ARGS{Content}) {
<& /Elements/MessageBox, QueueObj => $QueueObj, Default => $ARGS{Content}, IncludeSignature => 0, IncludeDefaultArticle => 0, FullWidth => 1 &>
% } elsif ( $QuoteTransaction ) {
<& /Elements/MessageBox, QueueObj => $QueueObj, QuoteTransaction => $QuoteTransaction, IncludeDefaultArticle => 0, FullWidth => 1 &>
% } else {
<& /Elements/MessageBox, QueueObj => $QueueObj, IncludeDefaultArticle => 1, FullWidth => 1 &>
%}
% $m->callback( %ARGS, QueueObj => $QueueObj, CallbackName => 'AfterMessageBox' );

        <& /Ticket/Elements/AddAttachments, %ARGS, QueueObj => $QueueObj, FullWidth => 1 &>
      </div>
    </&>
    <div class="form-row">
      <div class="col-12">
        <& /Elements/Submit, Label => loc("Create"), Name => 'SubmitTicket', id => 'SubmitTicket' &>
      </div>
    </div>
  </div>
</div>

<div id="Ticket-Create-details" class="hidden">
<a name="details"></a>
<div class="row">
<div class="boxcontainer col-md-6">
    <div class="ticket-info-basics">
          <&| /Widgets/TitleBox, title => loc('The Basics'), 
                title_class=> 'inverse',  
                color => "#993333" &>
<div>
<&| /Elements/LabeledValue, Label => loc('Priority') &>
    <& /Elements/SelectPriority,
      Name => "InitialPriority",
      Default => $ARGS{InitialPriority} ? $ARGS{InitialPriority} : $QueueObj->DefaultValue('InitialPriority'),
      QueueObj => $QueueObj,
  &>
</&>

<&| /Elements/LabeledValue, Label => loc('Final Priority'), Class => 'FinalPriority' &>
<& /Elements/SelectPriority,
    Name => "FinalPriority",
    Default => $ARGS{FinalPriority} ? $ARGS{FinalPriority} : $QueueObj->DefaultValue('FinalPriority'),
    QueueObj => $QueueObj,
  &>
</&>

<&| /Elements/LabeledValue, Label => loc('Time Estimated') &>
    <& /Elements/EditTimeValue, Name => 'TimeEstimated', Default => $ARGS{TimeEstimated} || '' &>
</&>

<&| /Elements/LabeledValue, Label => loc('Time Worked') &>
<& /Elements/EditTimeValue, Name => 'TimeWorked', Default => $ARGS{TimeWorked} || '' &>
</&>

<&| /Elements/LabeledValue, Label => loc('Time Left') &>
  <& /Elements/EditTimeValue, Name => 'TimeLeft', Default => $ARGS{TimeLeft} || '' &>
</&>
</div>
</&>

<div class="ticket-info-dates">
<&|/Widgets/TitleBox, title => loc("Dates"),
  title_class=> 'inverse',  
  color => "#663366" &>

<div>
<&| /Elements/LabeledValue, Label => loc('Starts') &>
    <& /Elements/SelectDate, Name => "Starts", Default => $ARGS{Starts} || $QueueObj->DefaultValue('Starts') || '' &>
</&>

<&| /Elements/LabeledValue, Label => loc('Due') &>
    <& /Elements/SelectDate, Name => "Due", Default => $ARGS{Due} || $QueueObj->DefaultValue('Due') || '' &>
</&>
<& /Elements/EditCustomFields,
    %ARGS,
    Object => $ticket,
    CategoryObj => $QueueObj,
    CustomFields => $QueueObj->TicketCustomFields,
    Grouping => 'Dates',
    InTable => 1,
    ForCreation => 1,
&>
</div>
</&>
</div>
</div>

</div>

<div class="boxcontainer col-md-6">
<div class="ticket-info-links">
<&| /Widgets/TitleBox, title => loc('Links'), title_class=> 'inverse' &>
<& /Elements/AddLinks,
    Object          => $ticket,
    CustomFields    => $QueueObj->TicketCustomFields,
    ARGSRef         => \%ARGS,
    &>
</&>
</div>

</div>
</div>
  <div class="form-row">
    <div class="col-12">
      <& /Elements/Submit, Label => loc("Create"), Name => 'SubmitTicket' &>
    </div>
  </div>
</div>
</form>

<%INIT>
$m->callback( CallbackName => "Init", ARGSRef => \%ARGS );
my $Queue = $ARGS{Queue};

# Use default queue from config site or user prefs if none provided
unless ($Queue) {
    $Queue = GetDefaultQueue();
}

# pick first in list in normal order unless queue provided from form/url/defaults
unless ($Queue) {
    my $cache_key = SetObjectSessionCache(
        ObjectType       => 'Queue',
        CheckRight       => 'CreateTicket',
        CacheNeedsUpdate => RT->System->QueueCacheNeedsUpdate,
        ShowAll          => 0,
    );

    $Queue = $session{$cache_key}{objects}[0]->{Id} if $session{$cache_key}{objects}[0];
}

Abort( loc( "Permission Denied" ) ) unless $Queue;

$session{DefaultQueue} = $Queue;

my $current_user = $session{'CurrentUser'};

if ($CloneTicket) {
    my $CloneTicketObj = RT::Ticket->new( $session{CurrentUser} );
    $CloneTicketObj->Load($CloneTicket)
        or Abort( loc("Ticket could not be loaded"), Code => HTTP::Status::HTTP_BAD_REQUEST );

    my $clone = {
        Requestors => join( ',', $CloneTicketObj->RequestorAddresses ),
        Cc         => join( ',', $CloneTicketObj->CcAddresses ),
        AdminCc    => join( ',', $CloneTicketObj->AdminCcAddresses ),
        InitialPriority => $CloneTicketObj->Priority,
    };

    $clone->{$_} = $CloneTicketObj->$_()
        for qw/Owner Subject FinalPriority Status/;

    $clone->{$_} = $CloneTicketObj->$_->AsString
        for grep { $CloneTicketObj->$_->IsSet }
        map      { $_ . "Obj" } qw/Starts Started Due Resolved/;

    my $get_link_value = sub {
        my ($link, $type) = @_;
        my $uri_method = $type . 'URI';
        my $local_method = 'Local' . $type;
        my $uri = $link->$uri_method;
        return if $uri->IsLocal and
                $uri->Object and
                $uri->Object->isa('RT::Ticket') and
                $uri->Object->__Value('Type') eq 'reminder';

        return $link->$local_method || $uri->URI;
    };
    my (@refers, @refers_by);
    my $refers = $CloneTicketObj->RefersTo;
    while ( my $refer = $refers->Next ) {
        my $refer_value = $get_link_value->($refer, 'Target');
        push @refers, $refer_value if defined $refer_value;
    }
    $clone->{'new-RefersTo'} = join ' ', @refers;

    my $refers_by = $CloneTicketObj->ReferredToBy;
    while ( my $refer_by = $refers_by->Next ) {
        my $refer_by_value = $get_link_value->($refer_by, 'Base');
        push @refers_by, $refer_by_value if defined $refer_by_value;
    }
    $clone->{'RefersTo-new'} = join ' ', @refers_by;

    my $cfs = $CloneTicketObj->QueueObj->TicketCustomFields();
    while ( my $cf = $cfs->Next ) {
        my $cf_id     = $cf->id;
        my $cf_values = $CloneTicketObj->CustomFieldValues( $cf->id );
        my @cf_values;
        while ( my $cf_value = $cf_values->Next ) {
            push @cf_values, $cf_value->Content;
        }

        next unless @cf_values;

        if ( @cf_values > 1 && $cf->Type eq 'Select' ) {
            $clone->{GetCustomFieldInputName( CustomField => $cf )} = \@cf_values;
        }
        else {
            if ( $cf->Type eq 'DateTime' ) {
                # Convert to user timezone. DateTime doesn't have multiple values, so only need to take care of
                # $cf_values[0]

                my $date = RT::Date->new( $session{'CurrentUser'} );
                $date->Set( Format => 'ISO', Value => $cf_values[0] );
                $cf_values[0] = $date->ISO( Timezone => 'user' );
            }

            $clone->{GetCustomFieldInputName( CustomField => $cf )} = join "\n",
              @cf_values;
        }
    }

    $m->callback( CallbackName => 'MassageCloneArgs', ARGSRef => $clone, Queue => $Queue );

    for ( keys %$clone ) {
        $ARGS{$_} = $clone->{$_} if not defined $ARGS{$_};
    }

}

# no support for muliple articles included at the same time
( $ARGS{IncludeArticleId} ) = grep defined && length, @{ $ARGS{IncludeArticleId} }
    if ref( $ARGS{IncludeArticleId} ) eq 'ARRAY';

my @results;

my $QueueObj = RT::Queue->new($current_user);
$QueueObj->Load($Queue) || Abort(loc("Queue [_1] could not be loaded.", $Queue||''), Code => HTTP::Status::HTTP_BAD_REQUEST);

my $title = loc("Create a new ticket in [_1]", $m->scomp("/Ticket/Elements/ShowQueue", QueueObj => $QueueObj, Escape => 0));

$m->callback( QueueObj => $QueueObj, title => \$title, results => \@results, ARGSRef => \%ARGS );

$m->scomp( '/Articles/Elements/SubjectOverride', ARGSRef => \%ARGS, QueueObj => $QueueObj, results => \@results );

$QueueObj->Disabled && Abort(loc("Cannot create tickets in a disabled queue."), Code => HTTP::Status::HTTP_NOT_FOUND);

if ( $ARGS{QueueChanged} ) {
    my %changed = map { $_ => 1 } ref $ARGS{ChangedField} eq 'ARRAY' ? @{ $ARGS{ChangedField} } : $ARGS{ChangedField}
        if $ARGS{ChangedField};

    for my $field ( qw/Content InitialPriority FinalPriority SLA Starts Due/, grep { /-CustomField-/ && !/-Magic$/ } sort keys %ARGS ) {
        if ( !$changed{$field} ) {
            delete $ARGS{$field};
            delete $DECODED_ARGS->{$field}; # EditCustomField uses $DECODED_ARGS
        }
    }
}

my $ticket = RT::Ticket->new($current_user); # empty ticket object

ProcessAttachments(ARGSRef => \%ARGS);

my $checks_failure = 0;

{
    my ($status, @msg) = $m->comp(
        '/Elements/ValidateCustomFields',
        CustomFields    => $QueueObj->TicketCustomFields,
        ARGSRef         => \%ARGS
    );
    unless ($status) {
        $checks_failure = 1;
        push @results, @msg;
    }

    ( $status, @msg ) = PreprocessTimeUpdates( \%ARGS );
    unless ( $status ) {
        push @results, @msg;
        $checks_failure = 1;
    }
}

my $gnupg_widget = $m->comp('/Elements/Crypt/SignEncryptWidget:new', Arguments => \%ARGS );
$m->comp( '/Elements/Crypt/SignEncryptWidget:Process',
    self      => $gnupg_widget,
    QueueObj  => $QueueObj,
);


if ( $ARGS{SubmitTicket} ) {
    my $status = $m->comp('/Elements/Crypt/SignEncryptWidget:Check',
        self      => $gnupg_widget,
        Operation => 'Create',
        QueueObj  => $QueueObj,
    );
    $checks_failure = 1 unless $status;
}

if ($ARGS{IncludeArticleId}) {
    my $article = RT::Article->new($session{'CurrentUser'});
    my ($ret, $msg) = $article->Load( $ARGS{IncludeArticleId} );

    if (!$ret) {
        # Make sure we don't finalize the ticket if we won't be able to include the article
        $checks_failure = 1;
        push @results, loc('Unable to load article "[_1]"', $ARGS{IncludeArticleId});
    }
}

# check email addresses for RT's
{
    my $custom_roles = $QueueObj->CustomRoles;
    my @static_roles = qw(Requestors Cc AdminCc);

    while (my $role = shift @static_roles || $custom_roles->Next) {
        my $field = ref $role ? $role->GroupType : $role;
        my $value = $ARGS{ $field };
        next unless defined $value && length $value;

        my ( $filtered_emails, $error_msgs ) = ProcessEmailAddresses(
            Field   => $field,
            Value   => $value,
            Label   => ref $role ? $role->Name : loc($role =~ /^(.*?)s?$/),
            ARGSRef => \%ARGS,
        );

        if ( @$error_msgs ) {
            push @results, @$error_msgs;
            $checks_failure ||= 1;
        }
        $ARGS{ $field } = join ', ', grep defined, @$filtered_emails;
    }
}

my $skip_create = 0;
$m->callback( CallbackName => 'BeforeCreate', ARGSRef => \%ARGS, skip_create => \$skip_create, 
              checks_failure => $checks_failure, results => \@results );

$m->comp( '/Articles/Elements/CheckSkipCreate', ARGSRef => \%ARGS, skip_create => \$skip_create,
              checks_failure => $checks_failure, results => \@results );

if ( !$checks_failure && !$skip_create && $ARGS{SubmitTicket} ) {
    $m->comp('Display.html', %ARGS);
    $RT::Logger->crit("After display call; error is $@");
    $m->abort();
}
PageMenu->child( basics => raw_html =>  q[<a href="#basics" onclick="return switchVisibility('Ticket-Create-basics','Ticket-Create-details');">] . loc('Basics') . q[</a>]);
PageMenu->child( details => raw_html =>  q[<a href="#details" onclick="return switchVisibility('Ticket-Create-details','Ticket-Create-basics');">] . loc('Details') . q[</a>]);
</%INIT>

<%ARGS>
$DependsOn => undef
$DependedOnBy => undef
$MemberOf => undef
$QuoteTransaction => undef
$CloneTicket => undef
$AddGroupCc => undef
</%ARGS>
