%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2011 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<& /Elements/Header, Title  => $title &>
<& /Elements/Tabs &>

% $m->callback(CallbackName => 'BeforeActionList', ARGSRef => \%ARGS, Ticket => $TicketObj);
<& /Elements/ListActions, actions => \@results &>

<form action="Update.html" name="TicketUpdate"
    method="post" enctype="multipart/form-data">
% $m->callback( CallbackName => 'FormStart', ARGSRef => \%ARGS, Ticket => $TicketObj, CanRespond => $CanRespond, CanComment => $CanComment, ResponseDefault => $ResponseDefault, CommentDefault => $CommentDefault );
<input type="hidden" class="hidden" name="QuoteTransaction" value="<% $ARGS{QuoteTransaction}||'' %>" />
<input type="hidden" class="hidden" name="DefaultStatus" value="<% $DefaultStatus ||''%>" />
<input type="hidden" class="hidden" name="Action" value="<% $ARGS{Action}||'' %>" />

<& /Elements/GnuPG/SignEncryptWidget:ShowIssues, self => $gnupg_widget &>

<div id="ticket-update-metadata">
  <&|/Widgets/TitleBox, title => loc('Ticket and Transaction') &>
<table width="100%" border="0">
% $m->callback(CallbackName => 'AfterTableOpens', ARGSRef => \%ARGS, Ticket => $TicketObj);

% my $skip;
% $m->callback( %ARGS, CallbackName => 'BeforeUpdateType', skip => \$skip );
% if (!$skip) {
<input type="hidden" class="hidden" name="id" value="<%$TicketObj->Id%>" /><br />
% }
<tr><td class="label"><&|/l&>Update Type</&>:</td>
<td><select name="UpdateType" id="UpdateType">
% if ($CanComment) {
<option value="private" <% ($ARGS{'UpdateType'} &&  $ARGS{'UpdateType'} eq "private") ? qq[ selected="selected"] : !$ARGS{'UpdateType'}&&$CommentDefault |n %>><&|/l&>Comments (Not sent to requestors)</&></option>
% }
% if ($CanRespond) {
<option value="response" <% ($ARGS{'UpdateType'} && $ARGS{'UpdateType'} eq "response") ? qq[ selected="selected"] : !$ARGS{'UpdateType'}&&$ResponseDefault |n %>><&|/l&>Reply to requestors</&></option>
% }
</select> 

<script type="text/javascript">
    jQuery(function() {
        jQuery("#UpdateType").change(function(ev) {
            jQuery(".messagebox-container")
                .removeClass("action-response action-private")
                .addClass("action-"+ev.target.value);
        });
    });
    jQuery(function() {
        jQuery("input[name=TxnSendMailTo]").change(function(ev) {
            jQuery("input[name=TxnSendMailTo][value="+ev.target.value+"]")
                  .attr("checked",jQuery(ev.target).attr('checked'));
        });
    });
</script>

% $m->callback( %ARGS, CallbackName => 'AfterUpdateType' );
</td></tr>

<& /Ticket/Elements/EditBasics,
    TicketObj => $TicketObj,
    InTable   => 1,
    fields    => [
        {   name => 'Status',
            comp => '/Elements/SelectStatus',
            args => {
                Name => 'Status',
                DefaultLabel => loc("[_1] (Unchanged)", loc($TicketObj->Status)),
                Default => $ARGS{'Status'} || ($TicketObj->Status eq $DefaultStatus ? undef : $DefaultStatus),
                TicketObj => $TicketObj,
                QueueObj => $TicketObj->QueueObj
            },
        },
        {   name => 'Owner',
            comp => '/Elements/SelectOwner',
            args => {
                Name         => "Owner",
                TicketObj    => $TicketObj,
                QueueObj     => $TicketObj->QueueObj,
                DefaultLabel => loc("[_1] (Unchanged)", $m->scomp('/Elements/ShowUser', User => $TicketObj->OwnerObj)),
                Default      => $ARGS{'Owner'}
            }
        },
        {   name => 'Worked',
            comp => '/Elements/EditTimeValue',
            args => {
                Name => 'UpdateTimeWorked',
                Default => $ARGS{UpdateTimeWorked}||'',
                InUnits => $ARGS{'UpdateTimeWorked-TimeUnits'}||'minutes',
            }
        },
    ]
&>

% $m->callback( %ARGS, CallbackName => 'AfterWorked', Ticket => $TicketObj );

<& /Ticket/Elements/EditTransactionCustomFields, %ARGS, TicketObj => $TicketObj, AsTable => 1 &>

  </table>
  </&>
</div>

<div id="ticket-update-message">
  <& /Ticket/Elements/ShowSimplifiedRecipients, TicketObj => $TicketObj, %ARGS &>

  <&|/Widgets/TitleBox, title => loc('Message'), class => 'messagedetails' &>
  <table width="100%" border="0">
<& /Ticket/Elements/UpdateCc, %ARGS, TicketObj => $TicketObj &>

% if ( $gnupg_widget ) {
<tr><td>&nbsp;</td><td>
<& /Elements/GnuPG/SignEncryptWidget,
    self => $gnupg_widget,
    TicketObj => $TicketObj,
&>
</td></tr>
% }
% $m->callback( %ARGS, CallbackName => 'AfterGnuPG' );

<tr><td class="label"><&|/l&>Subject</&>:</td><td> <input type="text" name="UpdateSubject" value="<% $ARGS{UpdateSubject} || $TicketObj->Subject || '' %>" />
% $m->callback( %ARGS, CallbackName => 'AfterSubject' );
</td></tr>

<tr><td class="label" valign="top"><&|/l&>Message</&>:</td>
<td class="messagebox-container action-<% $type %>">
<& /Articles/Elements/BeforeMessageBox, %ARGS &>
% $m->callback( %ARGS, CallbackName => 'BeforeMessageBox' );
% if (exists $ARGS{UpdateContent}) {
% # preserve QuoteTransaction so we can use it to set up sane references/in/reply to
% my $temp = $ARGS{'QuoteTransaction'};
% delete $ARGS{'QuoteTransaction'};
<& /Elements/MessageBox, Name=>"UpdateContent", Default=>$ARGS{UpdateContent}, IncludeSignature => 0, %ARGS&>
% $ARGS{'QuoteTransaction'} = $temp;
% } else {
% my $IncludeSignature = 1;
% $IncludeSignature = 0 if $Action ne 'Respond' && !RT->Config->Get('MessageBoxIncludeSignatureOnComment');
<& /Elements/MessageBox, Name=>"UpdateContent", IncludeSignature => $IncludeSignature, %ARGS &>
% }
</td></tr>

    <& /Ticket/Elements/AddAttachments, %ARGS, TicketObj => $TicketObj &>
  </table>
</&>

  <& /Elements/Submit, Label => loc('Update Ticket'), Name => 'SubmitTicket', id => 'SubmitTicket' &>

% if ($TicketObj->CurrentUserHasRight('ShowOutgoingEmail')) {
  <&|/Widgets/TitleBox, title => loc('Scrips and Recipients'), id => 'previewscrips', rolledup => RT->Config->Get('SimplifiedRecipients', $session{'CurrentUser'}) &>
    <& /Ticket/Elements/PreviewScrips, TicketObj => $TicketObj, %ARGS &>
  </&>
% }
</div>

% if (my $recips = $m->notes("DryRun-Recipients-".$TicketObj->Id)) {
<input type="hidden" name="TxnRecipients" value="<% join ",",sort keys %{$recips} %>" />
% }

</form>
<hr class="clear" />
<%INIT>
my $CanRespond = 0;
my $CanComment = 0;
my $checks_failure = 0;

my $TicketObj = LoadTicket($id);

my @results;

$m->callback( Ticket => $TicketObj, ARGSRef => \%ARGS, checks_failure => \$checks_failure, results => \@results, CallbackName => 'Initial' );

unless($DefaultStatus){
    $DefaultStatus=($ARGS{'Status'} ||$TicketObj->Status());
}

my $title = loc("Update ticket #[_1] ([_2])", $TicketObj->id, $TicketObj->Subject||'');

# Things needed in the template - we'll do the processing here, just
# for the convenience:

my ($CommentDefault, $ResponseDefault);
if ($Action ne 'Respond') {
    $CommentDefault = qq[ selected="selected"]; 
    $ResponseDefault = "";
} else {
    $CommentDefault = ""; 
    $ResponseDefault = qq[ selected="selected"];
}

my $type =             $ARGS{'UpdateType'} ? $ARGS{'UpdateType'} :
           lc $ARGS{'Action'} eq 'respond' ? 'response'          :
           lc $ARGS{'Action'} eq 'comment' ? 'private'           :
                                             'none'              ;


$CanRespond = 1 if ( $TicketObj->CurrentUserHasRight('ReplyToTicket') or
                     $TicketObj->CurrentUserHasRight('ModifyTicket') ); 

$CanComment = 1 if ( $TicketObj->CurrentUserHasRight('CommentOnTicket') or
                     $TicketObj->CurrentUserHasRight('ModifyTicket') ); 


# deal with deleting uploaded attachments
foreach my $key (keys %ARGS) {
    if ($key =~ m/^DeleteAttach-(.+)$/) {
        delete $session{'Attachments'}{$1};
    }
    $session{'Attachments'} = { %{$session{'Attachments'} || {}} };
}

# store the uploaded attachment in session
if ($ARGS{'Attach'}) {            # attachment?
    my $attachment = MakeMIMEEntity(
        AttachmentFieldName => 'Attach'
    );

    my $file_path = Encode::decode_utf8("$ARGS{'Attach'}");
    $session{'Attachments'} = {
        %{$session{'Attachments'} || {}},
        $file_path => $attachment,
    };
}

# delete temporary storage entry to make WebUI clean
unless (keys %{$session{'Attachments'}} and $ARGS{'UpdateAttach'}) {
    delete $session{'Attachments'};
}

my $gnupg_widget = $m->comp('/Elements/GnuPG/SignEncryptWidget:new', Arguments => \%ARGS );
$m->comp( '/Elements/GnuPG/SignEncryptWidget:Process',
    self => $gnupg_widget,
    TicketObj => $TicketObj,
);

if ( $ARGS{'SubmitTicket'} ) {

    my %checked = map {$_ => 1} grep {defined}
        (ref $ARGS{'TxnSendMailTo'} eq "ARRAY" ? @{$ARGS{'TxnSendMailTo'}}
             : defined $ARGS{'TxnSendMailTo'} ? ($ARGS{'TxnSendMailTo'}) : ());

    my @squelchlist = grep {not $checked{$_}} split /,/, ($ARGS{'TxnRecipients'}||'');
    $ARGS{'SquelchMailTo'} = \@squelchlist if @squelchlist;

    my $CFs = $TicketObj->TransactionCustomFields;
    my $ValidCFs = $m->comp(
        '/Elements/ValidateCustomFields',
        CustomFields => $CFs,
        NamePrefix => "Object-RT::Transaction--CustomField-",
        ARGSRef => \%ARGS
    );
    unless ( $ValidCFs ) {
        $checks_failure = 1;
        while (my $CF = $CFs->Next) {
            my $msg = $m->notes('InvalidField-' . $CF->Id) or next;
            push @results, loc($CF->Name) . ': ' . $msg;
        }
    }
    my $status = $m->comp('/Elements/GnuPG/SignEncryptWidget:Check',
        self      => $gnupg_widget,
        TicketObj => $TicketObj,
    );
    $checks_failure = 1 unless $status;
}

# check email addresses for RT's
{
    foreach my $field ( qw(UpdateCc UpdateBcc) ) {
        my $value = $ARGS{ $field };
        next unless defined $value && length $value;

        my @emails = Email::Address->parse( $value );
        foreach my $email ( grep RT::EmailParser->IsRTAddress($_->address), @emails ) {
            push @results, loc("[_1] is an address RT receives mail at. Adding it as a '[_2]' would create a mail loop", $email->format, loc(substr($field, 6)) );
            $checks_failure = 1;
            $email = undef;
        }
        $ARGS{ $field } = join ', ', map $_->format, grep defined, @emails;
    }
}
my $skip_update = 0;
$m->callback( CallbackName => 'BeforeUpdate', ARGSRef => \%ARGS, skip_update => \$skip_update,
              checks_failure => $checks_failure, results => \@results, TicketObj => $TicketObj );

if ( !$checks_failure && !$skip_update && exists $ARGS{SubmitTicket} ) {
    $m->callback( Ticket => $TicketObj, ARGSRef => \%ARGS, CallbackName => 'BeforeDisplay' );
    return $m->comp('Display.html', TicketObj => $TicketObj, %ARGS);
}
</%INIT>

<%ARGS>
$id => undef
$Action => undef
$DefaultStatus => undef
</%ARGS>
