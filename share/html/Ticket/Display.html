%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2024 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<& /Elements/Header, 
    Title => $title,
    TitleTrigger => 'ticketSubjectChanged from:body',
    TitleSource => RT->Config->Get('WebPath') . '/Views/Ticket/Title?id=' . $TicketObj->Id,
    LinkRel => \%link_rel &>
<& /Elements/Tabs &>

% $m->callback(CallbackName => 'BeforeActionList', %ARGS, Actions => \@Actions, ARGSRef => \%ARGS, Ticket => $TicketObj);

<& /Elements/ListActions, actions => \@Actions &>
<& Elements/ShowUpdateStatus, Ticket => $TicketObj &>
<div hx-trigger="ticketLinksChanged from:body" hx-get="<% RT->Config->Get('WebPath') %>/Views/Ticket/ShowDependencyStatus?id=<% $TicketObj->id %>">
  <& Elements/ShowDependencyStatus, Ticket => $TicketObj &>
</div>

<& /Elements/ShowWidgets, InRow => 0, Object => $TicketObj, Page => 'Display' &>

<%ARGS>
$TicketObj => undef
$ShowHeaders => 0
$HideUnsetFields => RT->Config->Get('HideUnsetFieldsOnDisplay', $session{CurrentUser})
$ForceShowHistory => 0
$InlineEdit => RT->Config->Get('InlineEdit', $session{CurrentUser})
</%ARGS>

<%INIT>

$m->callback( TicketObj => $TicketObj, ARGSRef => \%ARGS, CallbackName => 'Initial' );

if ( ! $ARGS{'NoRedirect'} && RT::Interface::Web->MobileClient()) {
    $ARGS{'id'} ||= $TicketObj->id if $TicketObj;
    RT::Interface::Web::Redirect(RT->Config->Get('WebURL').'m/ticket/show?id='.$ARGS{'id'});
    $m->abort;
}


my (@Actions, $title);


unless ($ARGS{'id'} || $TicketObj) {
    Abort('No ticket specified');
}

if ($ARGS{'id'} eq 'new') {
    # Create a new ticket

    my $Queue = RT::Queue->new( $session{'CurrentUser'} );
    $Queue->Load($ARGS{'Queue'});
    unless ( $Queue->id ) {
        Abort('Queue not found', Code => HTTP::Status::HTTP_NOT_FOUND);
    }

    unless ( $Queue->CurrentUserHasRight('CreateTicket') ) {
        Abort('You have no permission to create tickets in that queue.', Code => HTTP::Status::HTTP_FORBIDDEN);
    }

    ($TicketObj, @Actions) = CreateTicket( %ARGS );
    unless ( $TicketObj->CurrentUserHasRight('ShowTicket') ) {
        Abort("No permission to view newly created ticket #".$TicketObj->id.".", Code => HTTP::Status::HTTP_FORBIDDEN);
    }
} else { 
    $TicketObj ||= LoadTicket($ARGS{'id'});

    # fill ACL cache
    $TicketObj->CurrentUser->PrincipalObj->HasRights( Object => $TicketObj );

    my $SkipProcessing;

    $TicketObj->Atomic(sub{
        $m->callback( CallbackName => 'BeforeProcessArguments',
            TicketObj => $TicketObj,
            ActionsRef => \@Actions, ARGSRef => \%ARGS,
            SkipProcessing => \$SkipProcessing );

        my ($status, @msg) = $m->comp(
            '/Elements/ValidateCustomFields',
            Object       => $TicketObj,
            CustomFields => $TicketObj->CustomFields,
            ARGSRef      => \%ARGS,
        );
        unless ($status) {
            push @Actions, @msg;
            $SkipProcessing = 1;
        }
        return if $SkipProcessing;

        if ( defined $ARGS{'Action'} ) {
            if ($ARGS{'Action'} =~ /^(Steal|Delete|Take|Untake|SetTold)$/) {
                my $action = $1;
                my ($res, $msg) = $TicketObj->$action();
                push(@Actions, $msg);
            }
        }

        $m->callback(CallbackName => 'ProcessArguments',
                Ticket => $TicketObj,
                ARGSRef => \%ARGS,
                Actions => \@Actions);

        # It's common to change owner and add a reply/comment in the same
        # update. Process the owner change before the message update so the
        # new owner will see the message if they only see notifications when
        # they are the owner.
        push @Actions, ProcessTicketOwnerUpdate(ARGSRef => \%ARGS, TicketObj => $TicketObj );

        push @Actions, ProcessUpdateMessage(
            ARGSRef   => \%ARGS,
            Actions   => \@Actions,
            TicketObj => $TicketObj,
        );

        #Process status updates
        push @Actions, ProcessTicketWatchers(ARGSRef => \%ARGS, TicketObj => $TicketObj );
        push @Actions, ProcessTicketBasics(  ARGSRef => \%ARGS, TicketObj => $TicketObj );
        push @Actions, ProcessTicketDescription(  ARGSRef => \%ARGS, TicketObj => $TicketObj );
        push @Actions, ProcessTicketLinks(   ARGSRef => \%ARGS, TicketObj => $TicketObj );
        push @Actions, ProcessTicketDates(   ARGSRef => \%ARGS, TicketObj => $TicketObj );
        push @Actions, ProcessObjectCustomFieldUpdates(ARGSRef => \%ARGS, Object => $TicketObj );
        push @Actions, ProcessTicketReminders( ARGSRef => \%ARGS, TicketObj => $TicketObj );
    });
    if ( !$SkipProcessing ) {
        unless ($TicketObj->CurrentUserHasRight('ShowTicket')) {
            if (@Actions) {
                Abort("A change was applied successfully, but you no longer have permissions to view the ticket", Actions => \@Actions, Code => HTTP::Status::HTTP_FORBIDDEN);
            } else {
                Abort("No permission to view ticket", Code => HTTP::Status::HTTP_FORBIDDEN);
            }
        }
        if ( $ARGS{'MarkAsSeen'} ) {
            $TicketObj->SetAttribute(
                Name => 'User-'. $TicketObj->CurrentUser->id .'-SeenUpTo',
                Content => $TicketObj->LastUpdated,
            );
            push @Actions, loc('Marked all messages as seen');
        }
        $TicketObj->CurrentUser->AddRecentlyViewedTicket($TicketObj);
    }
}


$title = loc("#[_1]: [_2]", $TicketObj->Id, $TicketObj->Subject || '');

if ( $ARGS{'id'} and $ARGS{'id'} eq 'new' ) {
    if ( $ARGS{'AddGroupCc'} ){
        my $group = RT::Group->new($session{'CurrentUser'});
        my ($ret, $msg) = $group->LoadUserDefinedGroup($ARGS{'AddGroupCc'});

        unless ( $ret ){
            RT::Logger->warn("Unable to load group " . $ARGS{'AddGroupCc'} . ", $msg. Not adding as Cc.");
            return;
        }

        ( $ret, $msg ) = $TicketObj->AddWatcher(
            Type        => 'Cc',
            PrincipalId => $group->Id
        );

        RT::Logger->warn("Unable to add group " . $group->Name . ": " . $group->Id . " as a Cc: $msg")
            unless $ret;
    }
}

$m->callback(
    CallbackName => 'BeforeDisplay',
    TicketObj => \$TicketObj,
    Actions => \@Actions,
    title => \$title,
    ARGSRef => \%ARGS,
);

# This code does automatic redirection if any updates happen. 
MaybeRedirectForResults(
    Actions   => \@Actions,
    $TicketObj->Type eq 'approval' && RT->Config->Get('ForceApprovalsView')
        ? (Path      => "/Approvals/Display.html", Force => 1)
        : (Path      => "/Ticket/Display.html")
    ,
    Anchor    => $ARGS{'Anchor'},
    Arguments => { id => $TicketObj->id, exists $ARGS{ShowHistory} ? ( ShowHistory => $ARGS{ShowHistory} ) : () },
);

my %link_rel;
if (RT::Config->Get( 'ShowSearchNavigation', $session{'CurrentUser'} ) && defined $session{'collection-RT::Tickets'} and ($ARGS{'Query'} or $session{'CurrentSearchHash'}->{'Query'})) {
    my $item_map = $session{'collection-RT::Tickets'}->ItemMap;
    $link_rel{first} = "/Ticket/Display.html?id=" . $item_map->{first}                if $item_map->{$TicketObj->Id}{prev};
    $link_rel{prev}  = "/Ticket/Display.html?id=" . $item_map->{$TicketObj->Id}{prev} if $item_map->{$TicketObj->Id}{prev};
    $link_rel{next}  = "/Ticket/Display.html?id=" . $item_map->{$TicketObj->Id}{next} if $item_map->{$TicketObj->Id}{next};
    $link_rel{last}  = "/Ticket/Display.html?id=" . $item_map->{last}                 if $item_map->{$TicketObj->Id}{next} && $item_map->{last};
}

</%INIT>
