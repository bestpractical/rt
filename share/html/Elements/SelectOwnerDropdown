%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2018 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<select name="<%$Name%>" id="<%$Name%>">
%if ($DefaultValue) {
<option value=""<% !$Default ? qq[ selected="selected"] : '' |n %>><%$DefaultLabel |n%></option>
%}
%foreach my $UserRef ( @formatednames)  {
%my $User = $UserRef->[0];
<option <% ( $User->Id == $Default) ? qq[ selected="selected"] : '' |n %>
%if ($ValueAttribute eq 'id') {
    value="<%$User->id%>"
%} elsif ($ValueAttribute eq 'Name') {
    value="<%$User->Name%>"
%}
><% $UserRef->[1] %></option>
%}
</select>
<%INIT>
my %user_uniq_hash;

my $isSU = $session{CurrentUser}
    ->HasRight( Right => 'SuperUser', Object => $RT::System );
foreach my $object (@$Objects) {
    my $Users = RT::Users->new( $session{CurrentUser} );
    $Users->LimitToPrivileged;
    $Users->WhoHaveRight(
        Right               => 'OwnTicket',
        Object              => $object,
        IncludeSystemRights => 1,
        IncludeSuperusers   => $isSU
    );
    while ( my $User = $Users->Next() ) {
        $user_uniq_hash{ $User->Id() } = $User;
    }
}

my $dropdown_limit = RT->Config->Get( 'DropdownMenuLimit' ) || 50;
$m->callback( CallbackName => 'ModifyDropdownLimit', DropdownLimit => \$dropdown_limit );

if (keys(%user_uniq_hash) > $dropdown_limit ) {
    if ($Objects->[0]->id) {
        my $desc = $Objects->[0]->RecordType." ".$Objects->[0]->id;
        RT->Logger->notice("More than $dropdown_limit possible Owners found for $desc; switching to autocompleter.  See the \$AutocompleteOwners configuration option");
    }
    $m->comp("/Elements/SelectOwnerAutocomplete", %ARGS);
    return;
}

$m->callback( CallbackName => 'ModifyOwnerListRaw', ARGSRef => \%ARGS,
    UserHashRef => \%user_uniq_hash, DefaultRef => \$Default, Objects => $Objects );

if ($Default && $Default != RT->Nobody->id && !$user_uniq_hash{$Default}) {
    $user_uniq_hash{$Default} = RT::User->new($session{CurrentUser});
    $user_uniq_hash{$Default}->Load($Default);
}
$Default = 0 unless defined $Default && $Default =~ /^\d+$/;

my @formatednames = sort {lc $a->[1] cmp lc $b->[1]}
                     map {[$_, $_->Format]}
                    grep { $_->id != RT->Nobody->id }
                  values %user_uniq_hash;

my $nobody_user = RT::User->new( $session{CurrentUser} );
$nobody_user->Load( RT->Nobody->id );
my $nobody = [$nobody_user, $nobody_user->Format];
unshift @formatednames, $nobody;

$m->callback( CallbackName => 'ModifyOwnerListSorted', ARGSRef => \%ARGS,
    NamesRef => \@formatednames, DefaultRef => \$Default, Objects => $Objects );

</%INIT>

<%ARGS>
$Name => undef
$Objects => []
$Default => 0
$DefaultValue => 1
$DefaultLabel => "-"
$ValueAttribute => 'id'
</%ARGS>
