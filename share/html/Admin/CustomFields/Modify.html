%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2018 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<& /Admin/Elements/Header,
    Title => $title,
    Focus => (($added_cfv or $ARGS{FocusAddValue})
        ? "input[name=CustomField-$id-Value-new-Name]"
        : undef),
    &>
<& /Elements/Tabs &>
<& /Elements/ListActions, actions => \@results &>


<form method="post" action="Modify.html" name="ModifyCustomField" id="ModifyCustomField">
<input type="hidden" class="hidden" name="id" value="<% $id %>" />

<table>

<tr><td class="label"><&|/l&>Name</&></td>
<td><input name="Name" value="<% $CustomFieldObj->Name || $Name || '' %>" size="20" /></td></tr>

<tr><td class="label"><&|/l&>Description</&></td>
<td><input name="Description" value="<% $CustomFieldObj->Description || $Description || '' %>" size="80" /></td></tr>

<tr><td class="label"><&|/l&>Type</&></td>
<td><& /Admin/Elements/SelectCustomFieldType, 
        Name => "TypeComposite", 
        Default => $CustomFieldObj->TypeComposite, &>
</td></tr>

% if ( $CustomFieldObj->Id and $CustomFieldObj->HasRenderTypes ) {
<tr>
  <td class="label"><&|/l&>Render Type</&></td>
  <td>
    <& /Admin/Elements/SelectCustomFieldRenderType,
        Name            => "RenderType",
        TypeComposite   => $CustomFieldObj->TypeComposite,
        Default         => $CustomFieldObj->RenderType, 
        BasedOn         => $CustomFieldObj->BasedOnObj->id, &>
  </td>
</tr>
% }

% if ( $CustomFieldObj->Id and $CustomFieldObj->IsSelectionType and RT->Config->Get('CustomFieldValuesSources') and ( scalar(@{RT->Config->Get('CustomFieldValuesSources')}) > 0 ) ) {
<tr><td class="label"><&|/l&>Field values source:</&></td><td>
<& /Admin/Elements/EditCustomFieldValuesSource, CustomField => $CustomFieldObj &>
</td></tr>
% }

% if ( $CustomFieldObj->Id and $CustomFieldObj->IsCanonicalizeType and RT->Config->Get('CustomFieldValuesCanonicalizers') and ( scalar(@{RT->Config->Get('CustomFieldValuesCanonicalizers')}) > 0 ) ) {
<tr><td class="label"><&|/l&>Canonicalizer:</&></td><td>
<& /Admin/Elements/EditCustomFieldValuesCanonicalizer, CustomField => $CustomFieldObj &>
</td></tr>
% }

<tr><td class="label"><&|/l&>Applies to</&></td>
<td><& /Admin/Elements/SelectCustomFieldLookupType, 
        Name => "LookupType", 
        Default => $CustomFieldObj->LookupType || $LookupType, &>
</td></tr>

<script type="text/javascript">
jQuery( function() {
    var select = jQuery('select[name=TypeComposite]');
    var default_hint = select.find('option:selected').text();
    var hint_input = jQuery('input[name=EntryHint]');
    select.change(function() {
        var new_hint = jQuery(this).find('option:selected').text();
        if ( hint_input.val() == default_hint ) {
            hint_input.val(new_hint);
        }
        default_hint = new_hint;
    });
% if ( $id eq 'new' && not defined $EntryHint) {
    hint_input.val(default_hint);
% }
});
</script>

<tr><td class="label"><&|/l&>Entry Hint</&></td>
<td><input name="EntryHint" value="<% $CustomFieldObj->EntryHint // $EntryHint // '' %>" size="80" /></td></tr>


<tr class="edit_validation"><td class="label"><&|/l&>Validation</&></td>
<td><& /Widgets/ComboBox,
    Name    => 'Pattern',
    Default => $CustomFieldObj->Pattern || $Pattern,
    Size    => 20,
    Values  => \@CFvalidations,
&></td></tr>

% if ( $CustomFieldObj->SupportDefaultValues ) {
<tr class="edit_default_values"><td class="label"><&|/l, $CustomFieldObj->MaxValues &>Default [numerate,_1,value,values]</&></td>
<td>
<& /Elements/EditCustomField, NamePrefix => 'Default-', CustomField => $CustomFieldObj, ShowEmptyOption => 1 &>
</td>
</tr>
% }

<tr><td class="label"><&|/l&>Link values to</&></td><td>
<input size="60" name="LinkValueTo"  value="<% $CustomFieldObj->LinkValueTo || $LinkValueTo || '' %>" />
<div class="hints">
<&|/l&>RT can make this custom field's values into hyperlinks to another service.</&>
<&|/l&>Fill in this field with a URL.</&>
<&|/l_unsafe, '<tt>__id__</tt>', '<tt>__CustomField__</tt>' &>RT will replace [_1] and [_2] with the record's id and the custom field's value, respectively.</&>
</div></td></tr>

<tr><td class="label"><&|/l&>Include page</&></td><td>
<input size="60" name="IncludeContentForValue" value="<% $CustomFieldObj->IncludeContentForValue || $IncludeContentForValue || '' %>" />
<div class="hints">
<&|/l&>RT can include content from another web service when showing this custom field.</&>
<&|/l&>Fill in this field with a URL.</&>
<&|/l_unsafe, '<tt>__id__</tt>', '<tt>__CustomField__</tt>' &>RT will replace [_1] and [_2] with the record's id and the custom field's value, respectively.</&>
<i><&|/l&>Some browsers may only load content from the same domain as your RT server.</&></i>
</div></td></tr>

% if ( $CustomFieldObj->Id && $CustomFieldObj->IsSelectionType ) {
<tr class="categoriesbasedon"><td class="label"><&|/l&>Categories are based on</&></td><td>
<& /Admin/Elements/SelectCustomField,
    Name => "BasedOn",
    LookupType => $CustomFieldObj->LookupType,
    Default => $CustomFieldObj->BasedOnObj || $BasedOn,
    Not => $CustomFieldObj->id,
&>
</td></tr>
% }

<tr><td class="label">&nbsp;</td><td>
<input type="hidden" class="hidden" name="SetUniqueValues" value="1" />
<input type="checkbox" class="checkbox" id="UniqueValues" name="UniqueValues" value="1" <% $UniqueValuesChecked |n %> />
<label for="UniqueValues"><&|/l&>New values must be unique</&></label>
</td></tr>

% $m->callback(CallbackName => 'BeforeEnabled', CustomField => $CustomFieldObj, CFvalidations => \@CFvalidations);

<tr><td class="label">&nbsp;</td><td>
<input type="hidden" class="hidden" name="SetEnabled" value="1" />
<input type="checkbox" class="checkbox" id="Enabled" name="Enabled" value="1" <% $EnabledChecked |n %> />
<label for="Enabled"><&|/l&>Enabled (Unchecking this box disables this custom field)</&></label>
</td></tr>

% $m->callback(CallbackName => 'EndOfTable', CustomField => $CustomFieldObj, CFvalidations => \@CFvalidations);

</table>

% if ( $CustomFieldObj->Id && $CustomFieldObj->IsSelectionType && !$CustomFieldObj->IsExternalValues ) {
<h2><&|/l&>Values</&></h2>
<div>
<& /Admin/Elements/EditCustomFieldValues, CustomField => $CustomFieldObj &>
<& /Admin/Elements/AddCustomFieldValue, CustomField => $CustomFieldObj &>
</div>
% }

<& /Elements/Submit, Name => 'Update', Label => $id eq 'new'? loc('Create'): loc('Save Changes') &>

</form>
% $m->callback(%ARGS, CallbackName => 'EndOfPage', CustomFieldObj => $CustomFieldObj);
<%INIT>
my ($title, @results, $added_cfv);

my $CustomFieldObj = RT::CustomField->new( $session{'CurrentUser'} );

$m->callback(CallbackName => 'Initial', Pattern => \$Pattern, Results => \@results, ARGSRef => \%ARGS);

unless ( $id ) {
    $title = loc("Create a CustomField");
    $id    = 'new';
}
else {
    if ( $id eq 'new' ) {
        my ( $val, $msg ) = $CustomFieldObj->Create(
            Name          => $Name,
            TypeComposite => $TypeComposite,
            LookupType    => $LookupType,
            Description   => $Description,
            Pattern       => $Pattern,
            LinkValueTo   => $LinkValueTo,
            IncludeContentForValue => $IncludeContentForValue,
            BasedOn       => $BasedOn,
            Disabled      => ($Enabled ? 0 : 1),
            EntryHint     => $EntryHint,
            UniqueValues  => $UniqueValues,
        );
        if (!$val) {
            push @results, loc("Could not create CustomField: [_1]", $msg);
            $title = loc( 'Create a CustomField');
        }
        else {
            push @results, loc("Object created");
            $title = loc( 'Created CustomField [_1]', $CustomFieldObj->Name );
        }
    } else {
        push @results, loc('No CustomField')
            unless  $CustomFieldObj->Load( $id );

        $title = loc( 'Editing CustomField [_1]', $CustomFieldObj->Name );
    }
}

if ( $ARGS{'Update'} && $id ne 'new' ) {

    #we're asking about enabled on the web page but really care about disabled.
    $ARGS{'Disabled'} = $Enabled? 0 : 1;

    # make sure the unchecked checkbox still causes an update
    $ARGS{UniqueValues} ||= 0 if $SetUniqueValues;

    my @attribs = qw(Disabled Pattern Name TypeComposite LookupType Description LinkValueTo IncludeContentForValue EntryHint UniqueValues);

    push @results, UpdateRecordObject(
        AttributesRef => \@attribs,
        Object        => $CustomFieldObj,
        ARGSRef       => \%ARGS
    );
    if ( ($ValuesClass||'RT::CustomFieldValues') ne $CustomFieldObj->ValuesClass ) {
        my $original = $CustomFieldObj->ValuesClass;
        my ($good, $msg) = $CustomFieldObj->SetValuesClass( $ValuesClass );
        if ( $good ) {
            $msg = loc("[_1] changed from '[_2]' to '[_3]'",
                        loc("Field values source"), $original, $ValuesClass );
        }
        else {
            RT->Logger->debug("Unable to SetValuesClass to '$ValuesClass': $msg");
        }
        push @results, $msg;
    }
    if ( ($CanonicalizeClass||'') ne ($CustomFieldObj->CanonicalizeClass||'') ) {
        my $original = $CustomFieldObj->CanonicalizeClass;
        my ($good, $msg) = $CustomFieldObj->SetCanonicalizeClass( $CanonicalizeClass );
        if ( $good ) {
            # Improve message from class names to their friendly descriptions
            $original = $original->Description
                if $original && $original->require;
            $CanonicalizeClass = $CanonicalizeClass->Description
                if $CanonicalizeClass && $CanonicalizeClass->require;

            if (!$original) {
                $msg = loc("[_1] '[_2]' added",
                            loc("Canonicalizer"), $CanonicalizeClass);
            }
            elsif (!$CanonicalizeClass) {
                $msg = loc("[_1] '[_2]' removed",
                            loc("Canonicalizer"), $original);
            }
            else {
                $msg = loc("[_1] changed from '[_2]' to '[_3]'",
                            loc("Canonicalizer"), $original, $CanonicalizeClass );
            }
        }
        else {
            RT->Logger->debug("Unable to SetCanonicalizeClass to '$CanonicalizeClass': $msg");
        }

        push @results, $msg;
    }

    # Set the render type if we have it, but unset it if the new type doesn't
    # support render types
    if ( $CustomFieldObj->HasRenderTypes($TypeComposite) ) {
        my $original = $CustomFieldObj->RenderType;

        if ( defined $RenderType and $RenderType ne $original ) {
            # It's changed!  Let's update it.
            my ($good, $msg) = $CustomFieldObj->SetRenderType( $RenderType );

            if ( $good ) {
                $msg = loc("[_1] changed from '[_2]' to '[_3]'",
                            loc("Render Type"), $original, $RenderType );
            }
            else {
                RT->Logger->debug("Unable to SetRenderType to '$RenderType': $msg");
            }

            push @results, $msg;
        }
    }
    else {
        # Delete it if we no longer support render types
        $CustomFieldObj->SetRenderType( undef );
    }

    if (($CustomFieldObj->BasedOn||'') ne ($BasedOn||'')) {
        my ($good, $msg) = $CustomFieldObj->SetBasedOn( $BasedOn );
        push @results, $msg;
    }

    if ( $CustomFieldObj->SupportDefaultValues ) {
        my ($ret, $msg) = $CustomFieldObj->SetDefaultValues(
            Object => RT->System,
            Values => $ARGS{'Default-' . $CustomFieldObj->id . '-Value'} // $ARGS{'Default-' . $CustomFieldObj->id . '-Values'},
        );
        push @results, $msg;
    }

    my $paramtag = "CustomField-". $CustomFieldObj->Id ."-Value";
    # Delete any fields that want to be deleted
    foreach my $key ( keys %ARGS ) {
        next unless $key =~ /^Delete-$paramtag-(\d+)$/;
        my ($val, $msg) = $CustomFieldObj->DeleteValue( $1 );
        push (@results, $msg);
    }

    # Update any existing values
    my $values = $CustomFieldObj->ValuesObj;
    while ( my $value = $values->Next ) {
        foreach my $attr (qw(Name Description SortOrder Category)) {
            my $param = join("-", $paramtag, $value->Id, $attr);
            next unless exists $ARGS{$param};
            $ARGS{$param} =~ s/^\s+//;
            $ARGS{$param} =~ s/\s+$//;
            next if ($value->$attr()||'') eq ($ARGS{$param}||'');

            my $mutator = "Set$attr";
            my ($id, $msg) = $value->$mutator( $ARGS{$param} );
            push (@results, $msg);
        }
        $m->callback(CallbackName => 'AfterUpdateCustomFieldValue', CustomFieldObj => $CustomFieldObj, CustomFieldValueObj => $value, ARGSRef => \%ARGS );
    }

    # Add any new values
    if ( defined $ARGS{ $paramtag ."-new-Name" } && length $ARGS{ $paramtag ."-new-Name" } ) {
        my ($id, $msg) = $CustomFieldObj->AddValue(
            map { 
                $ARGS{$paramtag."-new-$_"} =~ s/^\s+//;
                $ARGS{$paramtag."-new-$_"} =~ s/\s+$//;
                $_ => $ARGS{ $paramtag ."-new-$_" } } grep { defined $ARGS{ $paramtag ."-new-$_" } } qw/ Name Description SortOrder Category/
        );
        push (@results, $msg);
        $added_cfv = 1 if $id;

        my $cfv = RT::CustomFieldValue->new( $session{CurrentUser} );
        $cfv->Load($id);
        $m->callback(CallbackName => 'AfterCreateCustomFieldValue',
CustomFieldObj => $CustomFieldObj, CustomFieldValueObj => $cfv, ARGSRef => \%ARGS );
    }
}

if ( $CustomFieldObj->id && $CustomFieldObj->IsOnlyGlobal ) {
    my ( $ret, $msg );
    my $object = $CustomFieldObj->RecordClassFromLookupType->new( $session{'CurrentUser'} );

    if ( $CustomFieldObj->Disabled && $CustomFieldObj->IsGlobal ) {
        ( $ret, $msg ) = $CustomFieldObj->RemoveFromObject($object);
    }
    elsif ( !$CustomFieldObj->Disabled && !$CustomFieldObj->IsGlobal ) {
        ( $ret, $msg ) = $CustomFieldObj->AddToObject($object);
    }

    # successful msg("object created" or "object deleted ) is useless here
    push @results, $msg unless $ret;
}

$id = $CustomFieldObj->id if $CustomFieldObj->id;

# This code does automatic redirection if any updates happen.
MaybeRedirectForResults(
    Actions     => \@results,
    Arguments   => { id => $id, FocusAddValue => ($added_cfv ? 1 : 0) },
) if $CustomFieldObj->id;


my $EnabledChecked = qq[checked="checked"];
$EnabledChecked = '' if $CustomFieldObj->Disabled;

my $UniqueValuesChecked = qq[checked="checked"];
$UniqueValuesChecked = '' if !$CustomFieldObj->UniqueValues;

my @CFvalidations = (
    '(?#Mandatory).',
    '(?#Digits)^[\d.]+$',
    '(?#Year)^[12]\d{3}$',
);

$m->callback(CallbackName => 'ValidationPatterns', Values => \@CFvalidations);

</%INIT>
<%ARGS>
$id => undef
$TypeComposite => undef
$LookupType => RT::Ticket->CustomFieldLookupType
$MaxValues => undef
$SortOrder => undef
$Description => undef
$Pattern => undef
$Name => undef
$SetEnabled => undef
$Enabled => 0
$SetUniqueValues => undef
$UniqueValues => 0
$ValuesClass => 'RT::CustomFieldValues'
$CanonicalizeClass => undef
$RenderType => undef
$LinkValueTo => undef
$IncludeContentForValue => undef
$BasedOn => undef
$EntryHint => undef
</%ARGS>
