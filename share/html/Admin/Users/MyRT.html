%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2019 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<& /Admin/Elements/Header, Title => $title  &>
<& /Elements/Tabs &>
<& /Elements/ListActions, actions => \@actions &>

<form method="post" class="mx-auto max-width-lg" action="<% RT->Config->Get('WebPath')  %>/Helpers/UpdateDashboard" data-dashboard_id = "MyRT" data-user_id = '<% $id %>'>
  <& /Widgets/SearchSelection,
                    pane_name => \%pane_name,
                    sections  => \@sections,
                    selected  => \%selected,
                    filters   => \@filters,
    &>
<input type="hidden" name="id" value="<% $id %>"/>
<& /Elements/Submit, Name => "UpdateSearches", Label => loc('Save') &>
</form>
<form method="post" action="MyRT.html?id=<% $id %>">
  <input type="hidden" name="Reset" value="1" />
  <& /Elements/Submit, Label => loc('Reset to default') &>
</form>

<%init>
my @actions;
my $UserObj = RT::User->new($session{'CurrentUser'});
$UserObj->Load($id) || Abort("Couldn't load user '" . ($id || '') . "'");
my $user = RT::User->new($session{'CurrentUser'});
my $title = loc("RT at a glance for the user [_1]", $UserObj->Name);

if ($ARGS{Reset}) {
    for my $pref_name ('HomepageSettings', 'SummaryRows') {
        next unless $UserObj->Preferences($pref_name);
        my ($ok, $msg) = $UserObj->DeletePreferences($pref_name);
        push @actions, $msg unless $ok;
    }
    push @actions, loc('Preferences saved for user [_1].', $UserObj->Name) unless @actions;
}

my $portlets = $UserObj->Preferences('HomepageSettings');
unless ($portlets) {
    my ($defaults) = RT::System->new($session{'CurrentUser'})->Attributes->Named('HomepageSettings');
    $portlets = $defaults ? $defaults->Content : {};
}

my @sections;
my %item_for;

my @components = map { type => "component", name => $_, label => loc($_) }, @{RT->Config->Get('HomepageComponents')};
$item_for{ $_->{type} }{ $_->{name} } = $_ for @components;

push @sections, {
      id    => 'components',
      label => loc("Components"),
      items => \@components,
    };
my $sys = RT::System->new($session{'CurrentUser'});
$sys->Load($id) || Abort("Couldn't load user '" . ($id || '') . "'");
my @objs = ($sys);
push @objs, RT::SavedSearch->new ($session{'CurrentUser'})->ObjectsForLoading
    if $session{'CurrentUser'}->HasRight( Right  => 'LoadSavedSearch',
                                          Object => $RT::System );
for my $object (@objs) {
     my @items;
     my $object_id = ref($object) . '-' . $object->Id;
     $object_id = 'system' if $object eq $sys;

     for ($m->comp("/Search/Elements/SearchesForObject", Object => $object)) {
         my ($desc, $loc_desc, $search) = @$_;

         my $SearchType = 'Ticket';
         if ((ref($search->Content)||'') eq 'HASH') {
             $SearchType = $search->Content->{'SearchType'}
                 if $search->Content->{'SearchType'};
         }
         else {
           $RT::Logger->debug("Search ".$search->id." ($desc) appears to have  no Content");
           }

         my $item;
         if ($object eq $sys && $SearchType eq 'Ticket') {
             $item = { type => 'system', name => $desc, label => $loc_desc };
         }
         else {
             my $oid = $object_id.'-SavedSearch-'.$search->Id;
             $item = { type => 'saved', name => $oid, search_type => $SearchType, label => $loc_desc };
           }
           $item_for{ $item->{type} }{ $item->{name} } = $item;
           push @items, $item;
     }

     my $label = $object eq $sys           ? loc('System')
               : $object->isa('RT::Group') ? $object->Label
                                           : $object->Name;
     push @sections, {
         id    => $object_id,
         label => $label,
         items => [ sort { lc($a->{label}) cmp lc($b->{label}) } @items ],
     };
 }

my %selected;
for my $pane (keys %$portlets) {
    my @items;

    for my $saved (@{ $portlets->{$pane} }) {
        my $item = $item_for{ $saved->{type} }{ $saved->{name} };
        if ($item) {
            push @items, $item;
        }
        else {
            push @actions, loc('Unable to find [_1] [_2]', $saved->{type}, $saved->{name});
        }
    }
    $selected{$pane} = \@items;
    }

my %pane_name = (
  'body'    => loc('Body'),
  'sidebar' => loc('Sidebar'),
);

my @filters = (
  [ 'component' => loc('Components') ],
  [ 'ticket'    => loc('Tickets') ],
  [ 'chart'     => loc('Charts') ],
);
$m->callback(
    CallbackName => 'Default',
    pane_name    => \%pane_name,
    sections     => \@sections,
    selected     => \%selected,
    filters      => \@filters,
);

</%INIT>
<%ARGS>
  $id => undef
</%ARGS>
