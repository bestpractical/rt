%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2024 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<& /Admin/Elements/Header, Title => loc("Catalog [_1]: Modify basics", $catalog->Name) &>
<& /Elements/Tabs &>
<& /Elements/ListActions, actions => \@results &>

<form method="post" enctype="multipart/form-data" id="ModifyCatalog" action="Modify.html" class="mx-auto max-width-lg">
  <input type="hidden" name="id" value="<% $catalog->id %>">
  <input type="hidden" name="CatalogChanged" value="0">

  <&| /Widgets/TitleBox, title => loc("Basics"), class => "catalog-basics", content_class => 'mx-auto width-sm' &>
    <& Elements/EditBasics, %ARGS, CatalogObj => $catalog &>
  </&>

  <&| /Widgets/TitleBox,
        title         => loc('Page Layouts'),
        class         => 'catalog-info-page-layouts',
        content_class => 'mx-auto width-sm',
  &>
    <& /Admin/Elements/EditPageLayoutMapping, Object => $catalog &>
  </&>

  <& /Elements/Submit, Label => loc('Save'), Name => "Update", FullWidth => 1 &>
</form>
<%args>
$id     => undef
$Update => 0
</%args>
<%init>
my @results;
my $catalog = LoadCatalog($id);

my $class        = 'RT::Asset';
my $page_layouts = RT->Config->Get('PageLayouts')->{$class};
my $mapping      = RT->Config->Get('PageLayoutMapping');
my $catalog_name = $catalog->Name;

if ($Update) {
    my @attributes = $catalog->WritableAttributes;

    # Disabled isn't submitted if unchecked, so use our hidden field to know if
    # it was included in the form.
    $ARGS{Disabled} = $ARGS{Disabled} ? 1 : 0
        if $ARGS{SetDisabled};

    push @results, UpdateRecordObject(
        Object          => $catalog,
        AttributesRef   => \@attributes,
        ARGSRef         => \%ARGS,
    );

    my $changed;

    for my $page ( sort keys %{ $mapping->{$class} } ) {
        my $layout = $ARGS{"Layout-$page"};

        my ($catalog_type) = grep { $_->{Type} eq 'Catalog' } @{ $mapping->{$class}{$page} || [] };
        if ($catalog_type) {
            if ($layout) {
                if ( ( $catalog_type->{Layout}{$catalog_name} // '' ) ne $layout ) {
                    $catalog_type->{Layout}{$catalog_name} = $layout;
                    $changed ||= 1;
                }
            }
            elsif ( $catalog_type->{Layout}{$catalog_name} ) {
                delete $catalog_type->{Layout}{$catalog_name};
                $changed ||= 1;
            }

            my @cleaned = grep { !ref $_->{Layout} || keys %{ $_->{Layout} } } @{ $mapping->{$class}{$page} };
            if ( @cleaned != @{ $mapping->{$class}{$page} } ) {
                @{ $mapping->{$class}{$page} } = @cleaned;
                $changed ||= 1;
            }
        }
        else {
            if ($layout) {
                unshift @{ $mapping->{$class}{$page} }, { Type => 'Catalog', Layout => { $catalog_name => $layout } };
                $changed ||= 1;
            }
        }
    }

    if ($changed) {
        my ( $ret, $msg ) = UpdateConfig( Name => 'PageLayoutMapping', Value => $mapping );
        if ($ret) {
            push @results, loc('Page Layouts updated');
        }
        else {
            push @results, $msg;
        }
    }

    MaybeRedirectForResults(
        Actions     => \@results,
        Arguments   => { id => $catalog->id },
    );
}
</%init>
